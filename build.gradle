plugins {
    id 'fi.jasoft.plugin.vaadin' version '1.1.6' apply false
}

//common for both projects
subprojects {
    ext.baseNameUpper = project.parent.name.capitalize()
    ext.baseNameLower = project.parent.name.toLowerCase()

    apply plugin: 'java'
    apply plugin: 'fi.jasoft.plugin.vaadin'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven {
            url "http://maven.vaadin.com/vaadin-addons"
        }
    }

    vaadin {
        version = "8.8.5"
        logToConsole = true
    }

    vaadinCompile {
        draftCompile = true
        strict = true
    }

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    group = "org.vaadin.risto.stylecalendar"

    //creates archives named "name-version.jar" and "name.war" where name is the name of the main project
    archivesBaseName = baseNameLower

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

//addon project, only creates the Directory-compatible JAR
project(":addon") {
    version = "2.3.0-vaadin8"

    vaadin {
        addon {
            styles = ["/VAADIN/addons/stylecalendar/stylecalendar.scss", "/VAADIN/addons/stylecalendar/stylecalendarfield.scss"]
            author = "Risto Yrjänä / Vaadin"
            license = "Apache License 2.0"
            title = baseNameUpper
        }
    }

    vaadinCompile {
        manageWidgetset = false
        widgetset = "${group}.StyleCalendarWidgetset"
    }

    dependencies {
        testCompile "junit:junit:4.12"
		
	    //compile group:'com.vaadin', name:'vaadin-compatibility-server', version:vaadin.version
	    compile group:'com.vaadin', name:'vaadin-compatibility-client', version:vaadin.version
		//compileOnly group:'com.vaadin', name:'vaadin-compatibility-client-compiled', version:vaadin.version
		//compileOnly group:'com.vaadin', name:'vaadin-compatibility-themes', version:vaadin.version
    }

    jar {
        from sourceSets.main.allJava //include sources
    }

    war.enabled = false
}

//demo project, builds addon and packages demo UI as WAR
project(":demo") {

    vaadinCompile.manageWidgetset = true

    vaadinRun {
        server = 'jetty'
        openInBrowser = false
    }

    dependencies {
        compile project(":addon")
        compile group: 'com.google.guava', name: 'guava', version: '21.0'
		
		//compileOnly group:'com.vaadin', name:'vaadin-client-compiler', version:vaadin.version
	    //compile group:'com.vaadin', name:'vaadin-compatibility-server', version:vaadin.version
		compileOnly group:'com.vaadin', name:'vaadin-compatibility-client', version:vaadin.version
		compileOnly group:'com.vaadin', name:'vaadin-compatibility-client-compiled', version:vaadin.version
		//compile group:'com.vaadin', name:'vaadin-compatibility-themes', version:vaadin.version
    }

    war.enabled = true
}

task(dist, type: Sync, dependsOn: [':addon:jar', ':demo:war']) {
    description = "Copies the addon JAR and the demo WAR to target/"
    File targetDir = mkdir("target")
    from project(":addon").jar.archivePath
    from project(":demo").war.archivePath
    into targetDir
}
